// Hawk's Eyes — Augmentation buff spell (new action system)
// Status effect kept in buffs_debuffs/hawks_eyes.dm
/datum/action/cooldown/spell/hawks_eyes
	button_icon = 'icons/mob/actions/mage_augmentation.dmi'
	name = "Hawk's Eyes"
	desc = "Sharpens the target's vision. (+5 Perception)\n\
	Casting on another person doubles the duration.\n\
	<b>Activation:</b> Hold middle-click to charge. Release when charged, then middle-click on target."
	button_icon_state = "hawks_eyes"
	sound = 'sound/magic/haste.ogg'
	spell_color = GLOW_COLOR_BUFF
	glow_intensity = GLOW_INTENSITY_LOW

	click_to_activate = TRUE
	cast_range = 7
	self_cast_possible = TRUE

	primary_resource_type = SPELL_COST_STAMINA
	primary_resource_cost = SPELLCOST_STAT_BUFF

	invocations = list("Oculi Accipitris.")
	invocation_type = INVOCATION_WHISPER

	charge_required = TRUE
	charge_time = 1 SECONDS
	charge_drain = 1
	charge_slowdown = CHARGING_SLOWDOWN_SMALL
	charge_sound = 'sound/magic/charging.ogg'
	charge_then_click = TRUE
	cooldown_time = 2 MINUTES

	associated_skill = /datum/skill/magic/arcane
	spell_tier = 2

	point_cost = 2

	spell_requirements = SPELL_REQUIRES_NO_ANTIMAGIC | SPELL_REQUIRES_HUMAN

/datum/action/cooldown/spell/hawks_eyes/cast(atom/cast_on)
	. = ..()
	var/mob/living/carbon/human/H = owner
	if(!istype(H))
		return FALSE

	if(!isliving(cast_on))
		to_chat(H, span_warning("That is not a valid target!"))
		return FALSE

	var/mob/living/spelltarget = cast_on

	if(spelltarget != H)
		H.visible_message("[H] mutters an incantation and [spelltarget]'s eyes glimmer.")
		to_chat(H, span_notice("With another person as a conduit, my spell's duration is doubled."))
		spelltarget.apply_status_effect(/datum/status_effect/buff/hawks_eyes/other)
	else
		H.visible_message("[H] mutters an incantation and their eyes glimmer.")
		spelltarget.apply_status_effect(/datum/status_effect/buff/hawks_eyes)

	return TRUE
